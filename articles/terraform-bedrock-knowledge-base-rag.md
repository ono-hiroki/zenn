---
title: "Terraformã§Amazon Bedrock Knowledge Baseã¨RAGã‚’æ§‹ç¯‰ã™ã‚‹"
emoji: "ğŸ¤–"
type: "tech"
topics: ["terraform", "aws", "bedrock", "rag", "opensearch"]
published: false
---

## ã¯ã˜ã‚ã«

Amazon Bedrock Knowledge Baseã‚’ä½¿ã†ã¨ã€S3ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã—ãŸãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’ãƒ™ã‚¯ãƒˆãƒ«åŒ–ã—ã€Claudeç­‰ã®LLMã§è‡ªç„¶è¨€èªã«ã‚ˆã‚‹è³ªå•å¿œç­”ãŒã§ãã‚‹RAGï¼ˆRetrieval-Augmented Generationï¼‰ã‚·ã‚¹ãƒ†ãƒ ã‚’æ§‹ç¯‰ã§ãã¾ã™ã€‚

ä»Šå›ã¯ã“ã®RAGã‚·ã‚¹ãƒ†ãƒ ã‚’Terraformã§æ§‹ç¯‰ã—ã¦ã¿ã¾ã—ãŸã€‚

## æ§‹ç¯‰ã™ã‚‹ã‚‚ã®

```mermaid
graph TB
    subgraph "Amazon Bedrock"
        KB[Knowledge Base]
        DS[Data Source]
        TI[Titan Embeddings<br/>ãƒ™ã‚¯ãƒˆãƒ«åŒ–]
        CL[Claude 3.5 Sonnet<br/>å›ç­”ç”Ÿæˆ]
    end

    subgraph "Storage"
        S3[S3 Bucket<br/>ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ]
        OS[OpenSearch Serverless<br/>ãƒ™ã‚¯ãƒˆãƒ«ã‚¹ãƒˆã‚¢]
    end

    S3 --> DS
    DS --> KB
    KB --> OS
    KB --> TI
    KB --> CL
```

| ã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆ | é¸æŠ |
|---------------|------|
| ãƒ™ã‚¯ãƒˆãƒ«DB | OpenSearch Serverless |
| ãƒªãƒ¼ã‚¸ãƒ§ãƒ³ | ap-northeast-1 (æ±äº¬) |
| Embedding Model | Amazon Titan Embed Text v1 |
| å›ç­”ç”Ÿæˆ | Claude 3.5 Sonnet |

## å‰ææ¡ä»¶

- Terraform >= 1.5.0
- AWS CLI v2
- Bedrockã®ãƒ¢ãƒ‡ãƒ«ã‚¢ã‚¯ã‚»ã‚¹ãŒæœ‰åŠ¹åŒ–ã•ã‚Œã¦ã„ã‚‹ã“ã¨

:::message
AWS Console > Bedrock > Model access ã‹ã‚‰ä»¥ä¸‹ã®ãƒ¢ãƒ‡ãƒ«ã‚’æœ‰åŠ¹åŒ–ã—ã¦ãŠãå¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚
- Amazon Titan Embeddings G1 - Text
- Anthropic Claude 3.5 Sonnet
  :::

## ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªæ§‹æˆ

```
bedrock/
â”œâ”€â”€ terraform.tf          # ãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼ãƒãƒ¼ã‚¸ãƒ§ãƒ³åˆ¶ç´„
â”œâ”€â”€ provider.tf           # AWSãƒ»OpenSearchãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼
â”œâ”€â”€ variables.tf          # å¤‰æ•°å®šç¾©
â”œâ”€â”€ locals.tf             # ãƒ­ãƒ¼ã‚«ãƒ«å¤‰æ•°
â”œâ”€â”€ iam.tf                # IAMãƒ­ãƒ¼ãƒ«ãƒ»ãƒãƒªã‚·ãƒ¼
â”œâ”€â”€ s3.tf                 # S3ãƒã‚±ãƒƒãƒˆ
â”œâ”€â”€ opensearch.tf         # OpenSearch Serverless
â”œâ”€â”€ bedrock.tf            # Knowledge Baseãƒ»Data Source
â”œâ”€â”€ outputs.tf            # å‡ºåŠ›å€¤
â”œâ”€â”€ terraform.tfvars      # å¤‰æ•°å€¤
â””â”€â”€ docs/                 # RAGç”¨ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ
```

## Terraformã‚³ãƒ¼ãƒ‰

:::details terraform.tf
```hcl:terraform.tf
terraform {
  required_version = ">= 1.5.0"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.48"
    }
    opensearch = {
      source  = "opensearch-project/opensearch"
      version = "= 2.2.0"
    }
    time = {
      source  = "hashicorp/time"
      version = "~> 0.11"
    }
  }
}
```
:::

:::message alert
`opensearch`ãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼ã¯`2.2.0`ã«å›ºå®šã—ã¦ãã ã•ã„ã€‚ä»–ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã§ã¯OpenSearch Serverlessã¨ã®èªè¨¼ã‚¨ãƒ©ãƒ¼ãŒç™ºç”Ÿã™ã‚‹å ´åˆãŒã‚ã‚Šã¾ã™ã€‚
:::

:::details provider.tf
```hcl:provider.tf
provider "aws" {
  region = var.aws_region

  default_tags {
    tags = {
      Project     = var.project_name
      Environment = var.environment
      ManagedBy   = "Terraform"
    }
  }
}

provider "opensearch" {
  url         = aws_opensearchserverless_collection.knowledge_base.collection_endpoint
  healthcheck = false
}
```
:::

:::details variables.tf
```hcl:variables.tf
variable "project_name" {
  description = "ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆå"
  type        = string
}

variable "knowledge_base_name" {
  description = "Knowledge Baseå"
  type        = string
}

variable "environment" {
  description = "ç’°å¢ƒå"
  type        = string
  default     = "sandbox"
}

variable "aws_region" {
  description = "AWSãƒªãƒ¼ã‚¸ãƒ§ãƒ³"
  type        = string
  default     = "ap-northeast-1"
}

variable "s3_bucket_name_prefix" {
  description = "S3ãƒã‚±ãƒƒãƒˆåã®ãƒ—ãƒ¬ãƒ•ã‚£ãƒƒã‚¯ã‚¹"
  type        = string
  default     = "bedrock-kb-docs"
}

variable "s3_force_destroy" {
  description = "S3ãƒã‚±ãƒƒãƒˆå‰Šé™¤æ™‚ã«ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚‚å‰Šé™¤ã™ã‚‹ã‹"
  type        = bool
  default     = true
}

variable "opensearch_collection_name" {
  description = "OpenSearch Serverlessã‚³ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³å"
  type        = string
  default     = "bedrock-kb-collection"
}

variable "opensearch_index_name" {
  description = "OpenSearchã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹å"
  type        = string
  default     = "bedrock-knowledge-base-default-index"
}

variable "embedding_model_id" {
  description = "Embedding Model ID"
  type        = string
  default     = "amazon.titan-embed-text-v1"
}

variable "vector_dimension" {
  description = "ãƒ™ã‚¯ãƒˆãƒ«æ¬¡å…ƒæ•°"
  type        = number
  default     = 1536
}

variable "iam_role_delay_seconds" {
  description = "IAMãƒãƒªã‚·ãƒ¼é©ç”¨å¾Œã®å¾…æ©Ÿæ™‚é–“ï¼ˆç§’ï¼‰"
  type        = number
  default     = 20
}
```
:::

:::details locals.tf
```hcl:locals.tf
data "aws_caller_identity" "current" {}
data "aws_partition" "current" {}
data "aws_region" "current" {}

data "aws_bedrock_foundation_model" "embedding" {
  model_id = var.embedding_model_id
}

locals {
  account_id = data.aws_caller_identity.current.account_id
  partition  = data.aws_partition.current.partition
  region     = data.aws_region.current.name

  region_tokens = split("-", local.region)
  region_short  = "${substr(local.region_tokens[0], 0, 2)}${substr(local.region_tokens[1], 0, 1)}${local.region_tokens[2]}"

  opensearch_field_mapping = {
    vector_field   = "bedrock-knowledge-base-default-vector"
    text_field     = "AMAZON_BEDROCK_TEXT_CHUNK"
    metadata_field = "AMAZON_BEDROCK_METADATA"
  }

  common_tags = {
    Project     = var.project_name
    Environment = var.environment
  }
}
```
:::

:::details s3.tf
```hcl:s3.tf
resource "aws_s3_bucket" "documents" {
  bucket        = "${var.s3_bucket_name_prefix}-${local.region_short}-${local.account_id}"
  force_destroy = var.s3_force_destroy

  tags = merge(local.common_tags, {
    Name = "${var.project_name}-documents"
  })
}

resource "aws_s3_bucket_server_side_encryption_configuration" "documents" {
  bucket = aws_s3_bucket.documents.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_versioning" "documents" {
  bucket = aws_s3_bucket.documents.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_public_access_block" "documents" {
  bucket = aws_s3_bucket.documents.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
```
:::

:::details iam.tf
```hcl:iam.tf
resource "aws_iam_role" "bedrock_kb" {
  name = "AmazonBedrockExecutionRoleForKnowledgeBase_${var.knowledge_base_name}"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "bedrock.amazonaws.com"
        }
        Condition = {
          StringEquals = {
            "aws:SourceAccount" = local.account_id
          }
          ArnLike = {
            "aws:SourceArn" = "arn:${local.partition}:bedrock:${local.region}:${local.account_id}:knowledge-base/*"
          }
        }
      }
    ]
  })
}

resource "aws_iam_role_policy" "bedrock_kb_model" {
  name = "AmazonBedrockFoundationModelPolicy_${var.knowledge_base_name}"
  role = aws_iam_role.bedrock_kb.name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action   = "bedrock:InvokeModel"
        Effect   = "Allow"
        Resource = data.aws_bedrock_foundation_model.embedding.model_arn
      }
    ]
  })
}

resource "aws_iam_role_policy" "bedrock_kb_s3" {
  name = "AmazonBedrockS3Policy_${var.knowledge_base_name}"
  role = aws_iam_role.bedrock_kb.name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action   = "s3:ListBucket"
        Effect   = "Allow"
        Resource = aws_s3_bucket.documents.arn
      },
      {
        Action   = "s3:GetObject"
        Effect   = "Allow"
        Resource = "${aws_s3_bucket.documents.arn}/*"
      }
    ]
  })
}

resource "aws_iam_role_policy" "bedrock_kb_opensearch" {
  name = "AmazonBedrockOSSPolicy_${var.knowledge_base_name}"
  role = aws_iam_role.bedrock_kb.name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action   = "aoss:APIAccessAll"
        Effect   = "Allow"
        Resource = aws_opensearchserverless_collection.knowledge_base.arn
      }
    ]
  })
}

resource "time_sleep" "iam_propagation" {
  create_duration = "${var.iam_role_delay_seconds}s"

  depends_on = [aws_iam_role_policy.bedrock_kb_opensearch]
}
```

:::message
`time_sleep`ãƒªã‚½ãƒ¼ã‚¹ã¯IAMãƒãƒªã‚·ãƒ¼ã®ä¼æ’­ã‚’å¾…æ©Ÿã™ã‚‹ãŸã‚ã®ã‚‚ã®ã§ã™ã€‚IAMã¯Eventual Consistencyã®ãŸã‚ã€ãƒãƒªã‚·ãƒ¼ä½œæˆç›´å¾Œã«Knowledge Baseã‚’ä½œæˆã™ã‚‹ã¨æ¨©é™ã‚¨ãƒ©ãƒ¼ã«ãªã‚‹å ´åˆãŒã‚ã‚Šã¾ã™ã€‚
:::
:::

OpenSearch Serverlessã®è¨­å®šã¯æœ€ã‚‚è¤‡é›‘ãªéƒ¨åˆ†ã§ã™ã€‚

:::details opensearch.tf
```hcl:opensearch.tf
# æš—å·åŒ–ãƒãƒªã‚·ãƒ¼ï¼ˆå¿…é ˆï¼šã‚³ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³ä½œæˆå‰ã«å¿…è¦ï¼‰
resource "aws_opensearchserverless_security_policy" "encryption" {
  name = var.opensearch_collection_name
  type = "encryption"

  policy = jsonencode({
    Rules = [
      {
        Resource     = ["collection/${var.opensearch_collection_name}"]
        ResourceType = "collection"
      }
    ]
    AWSOwnedKey = true
  })
}

# ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ãƒãƒªã‚·ãƒ¼
resource "aws_opensearchserverless_security_policy" "network" {
  name = var.opensearch_collection_name
  type = "network"

  policy = jsonencode([
    {
      Rules = [
        {
          ResourceType = "collection"
          Resource     = ["collection/${var.opensearch_collection_name}"]
        },
        {
          ResourceType = "dashboard"
          Resource     = ["collection/${var.opensearch_collection_name}"]
        }
      ]
      AllowFromPublic = true
    }
  ])
}

# ãƒ‡ãƒ¼ã‚¿ã‚¢ã‚¯ã‚»ã‚¹ãƒãƒªã‚·ãƒ¼
resource "aws_opensearchserverless_access_policy" "data_access" {
  name = var.opensearch_collection_name
  type = "data"

  policy = jsonencode([
    {
      Rules = [
        {
          ResourceType = "index"
          Resource     = ["index/${var.opensearch_collection_name}/*"]
          Permission = [
            "aoss:CreateIndex",
            "aoss:DeleteIndex",
            "aoss:DescribeIndex",
            "aoss:ReadDocument",
            "aoss:UpdateIndex",
            "aoss:WriteDocument"
          ]
        },
        {
          ResourceType = "collection"
          Resource     = ["collection/${var.opensearch_collection_name}"]
          Permission = [
            "aoss:CreateCollectionItems",
            "aoss:DescribeCollectionItems",
            "aoss:UpdateCollectionItems"
          ]
        }
      ]
      Principal = [
        aws_iam_role.bedrock_kb.arn,
        data.aws_caller_identity.current.arn
      ]
    }
  ])
}

# ã‚³ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³
resource "aws_opensearchserverless_collection" "knowledge_base" {
  name = var.opensearch_collection_name
  type = "VECTORSEARCH"

  depends_on = [
    aws_opensearchserverless_security_policy.encryption,
    aws_opensearchserverless_security_policy.network,
    aws_opensearchserverless_access_policy.data_access
  ]
}

# ãƒ™ã‚¯ãƒˆãƒ«ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹
resource "opensearch_index" "knowledge_base" {
  name                           = var.opensearch_index_name
  number_of_shards               = "2"
  number_of_replicas             = "0"
  index_knn                      = true
  index_knn_algo_param_ef_search = "512"

  mappings = jsonencode({
    properties = {
      "${local.opensearch_field_mapping.vector_field}" = {
        type      = "knn_vector"
        dimension = var.vector_dimension
        method = {
          name   = "hnsw"
          engine = "faiss"
          parameters = {
            m               = 16
            ef_construction = 512
          }
          space_type = "l2"
        }
      }
      "${local.opensearch_field_mapping.metadata_field}" = {
        type  = "text"
        index = false
      }
      "${local.opensearch_field_mapping.text_field}" = {
        type  = "text"
        index = true
      }
    }
  })

  force_destroy = true

  depends_on = [aws_opensearchserverless_collection.knowledge_base]
}
```
:::

:::details bedrock.tf
```hcl:bedrock.tf
resource "aws_bedrockagent_knowledge_base" "main" {
  name     = var.knowledge_base_name
  role_arn = aws_iam_role.bedrock_kb.arn

  knowledge_base_configuration {
    type = "VECTOR"
    vector_knowledge_base_configuration {
      embedding_model_arn = data.aws_bedrock_foundation_model.embedding.model_arn
    }
  }

  storage_configuration {
    type = "OPENSEARCH_SERVERLESS"
    opensearch_serverless_configuration {
      collection_arn    = aws_opensearchserverless_collection.knowledge_base.arn
      vector_index_name = var.opensearch_index_name
      field_mapping {
        vector_field   = local.opensearch_field_mapping.vector_field
        text_field     = local.opensearch_field_mapping.text_field
        metadata_field = local.opensearch_field_mapping.metadata_field
      }
    }
  }

  depends_on = [
    aws_iam_role_policy.bedrock_kb_model,
    aws_iam_role_policy.bedrock_kb_s3,
    opensearch_index.knowledge_base,
    time_sleep.iam_propagation
  ]
}

resource "aws_bedrockagent_data_source" "s3" {
  knowledge_base_id = aws_bedrockagent_knowledge_base.main.id
  name              = "${var.knowledge_base_name}-s3-datasource"

  data_source_configuration {
    type = "S3"
    s3_configuration {
      bucket_arn = aws_s3_bucket.documents.arn
    }
  }
}
```
:::

:::details outputs.tf
```hcl:outputs.tf
output "s3_bucket_name" {
  description = "ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚½ãƒ¼ã‚¹ç”¨S3ãƒã‚±ãƒƒãƒˆå"
  value       = aws_s3_bucket.documents.id
}

output "knowledge_base_id" {
  description = "Bedrock Knowledge Base ID"
  value       = aws_bedrockagent_knowledge_base.main.id
}

output "data_source_id" {
  description = "Bedrock Data Source ID"
  value       = aws_bedrockagent_data_source.s3.data_source_id
}
```
:::

:::details terraform.tfvars
```hcl:terraform.tfvars
project_name        = "bedrock-rag"
knowledge_base_name = "my-knowledge-base"
environment         = "sandbox"
```
:::

## ãƒ‡ãƒ—ãƒ­ã‚¤

### 1. Terraformã®å®Ÿè¡Œ

```bash
# åˆæœŸåŒ–
terraform init

# å®Ÿè¡Œè¨ˆç”»ã®ç¢ºèª
terraform plan

# ãƒªã‚½ãƒ¼ã‚¹ä½œæˆ
terraform apply
```

:::message
OpenSearch Serverlessã®ã‚³ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³ä½œæˆã¯ã€ã‚¢ã‚«ã‚¦ãƒ³ãƒˆåˆã®å ´åˆ**10åˆ†ä»¥ä¸Š**ã‹ã‹ã‚‹ã“ã¨ãŒã‚ã‚Šã¾ã™ã€‚
:::

### 2. ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã®ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰

```bash
# ã‚µãƒ³ãƒ—ãƒ«ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’ä½œæˆ
mkdir -p docs
cat << 'EOF' > docs/company-policy.md
# ä¼šç¤¾ãƒãƒªã‚·ãƒ¼

## ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯
- é€±3æ—¥ã¾ã§ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯å¯èƒ½
- äº‹å‰ç”³è«‹ã¯ä¸è¦ï¼ˆãƒãƒ¼ãƒ ã¸ã®å…±æœ‰ã¯å¿…é ˆï¼‰
- ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£ã‚¬ã‚¤ãƒ‰ãƒ©ã‚¤ãƒ³ã®éµå®ˆãŒå¿…è¦
EOF

# S3ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰
aws s3 cp ./docs/ s3://$(terraform output -raw s3_bucket_name)/ --recursive
```

### 3. ãƒ‡ãƒ¼ã‚¿ã‚½ãƒ¼ã‚¹ã®åŒæœŸ

```bash
# Ingestion Jobã‚’é–‹å§‹
aws bedrock-agent start-ingestion-job \
  --knowledge-base-id $(terraform output -raw knowledge_base_id) \
  --data-source-id $(terraform output -raw data_source_id)
```

## å‹•ä½œç¢ºèª

### æ¤œç´¢ã®ã¿

```bash
aws bedrock-agent-runtime retrieve \
  --knowledge-base-id $(terraform output -raw knowledge_base_id) \
  --retrieval-query '{"text": "ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯ã®ãƒ«ãƒ¼ãƒ«ã¯ï¼Ÿ"}'
```

### å›ç­”ç”Ÿæˆä»˜ãï¼ˆClaude 3.5 Sonnetï¼‰

```bash
aws bedrock-agent-runtime retrieve-and-generate \
  --input '{"text": "ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯ã®ãƒ«ãƒ¼ãƒ«ã‚’æ•™ãˆã¦ãã ã•ã„"}' \
  --retrieve-and-generate-configuration '{
    "type": "KNOWLEDGE_BASE",
    "knowledgeBaseConfiguration": {
      "knowledgeBaseId": "'$(terraform output -raw knowledge_base_id)'",
      "modelArn": "arn:aws:bedrock:ap-northeast-1::foundation-model/anthropic.claude-3-5-sonnet-20240620-v1:0"
    }
  }'
```

### å®Ÿè¡Œçµæœ

```json
{
  "output": {
    "text": "ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯ã«é–¢ã™ã‚‹ãƒ«ãƒ¼ãƒ«ã¯ä»¥ä¸‹ã®é€šã‚Šã§ã™ï¼š\n\n1. é€±3æ—¥ã¾ã§ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯ãŒå¯èƒ½ã§ã™ã€‚\n2. ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯ã®äº‹å‰ç”³è«‹ã¯ä¸è¦ã§ã™ãŒã€ãƒãƒ¼ãƒ ã¸ã®å…±æœ‰ã¯å¿…é ˆã¨ãªã£ã¦ã„ã¾ã™ã€‚\n3. ãƒªãƒ¢ãƒ¼ãƒˆãƒ¯ãƒ¼ã‚¯æ™‚ã«ã¯ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£ã‚¬ã‚¤ãƒ‰ãƒ©ã‚¤ãƒ³ã‚’éµå®ˆã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚"
  }
}
```

ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã®å†…å®¹ã‚’ã‚‚ã¨ã«ã€çš„ç¢ºãªå›ç­”ãŒè¿”ã£ã¦ãã¾ã—ãŸï¼

## TIPS

### 1. OpenSearchãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³

`opensearch-project/opensearch`ãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼ã¯`2.2.0`ã‚’ä½¿ç”¨ã—ã¦ãã ã•ã„ã€‚ä»–ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã§ã¯OpenSearch Serverlessã¨ã®èªè¨¼ã§ã‚¨ãƒ©ãƒ¼ã«ãªã‚Šã¾ã™ã€‚

### 2. IAMã®ä¼æ’­å¾…æ©Ÿ

IAMã¯Eventual Consistencyã®ãŸã‚ã€ãƒãƒªã‚·ãƒ¼ä½œæˆç›´å¾Œã«Knowledge Baseã‚’ä½œæˆã™ã‚‹ã¨ã‚¨ãƒ©ãƒ¼ã«ãªã‚‹ã“ã¨ãŒã‚ã‚Šã¾ã™ã€‚`time_sleep`ã§20ç§’ç¨‹åº¦å¾…æ©Ÿã‚’å…¥ã‚Œã¦ã„ã¾ã™ã€‚

### 3. OpenSearchã®healthcheck

OpenSearch Serverlessã§ã¯healthcheckãŒå‹•ä½œã—ãªã„ãŸã‚ã€`healthcheck = false`ã‚’è¨­å®šã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

### 4. ãƒ•ã‚£ãƒ¼ãƒ«ãƒ‰ãƒãƒƒãƒ”ãƒ³ã‚°ã®æ•´åˆæ€§

`opensearch_index`ã®ãƒãƒƒãƒ”ãƒ³ã‚°ã¨`aws_bedrockagent_knowledge_base`ã®`field_mapping`ãŒä¸€è‡´ã—ã¦ã„ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚

## ã‚³ã‚¹ãƒˆã«ã¤ã„ã¦

:::message alert
OpenSearch Serverlessã¯æœ€ä½4 OCUï¼ˆæ¤œç´¢2 + ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹2ï¼‰ã‹ã‚‰èª²é‡‘ãŒå§‹ã¾ã‚Šã¾ã™ã€‚æ¤œè¨¼å¾Œã¯`terraform destroy`ã§ãƒªã‚½ãƒ¼ã‚¹ã‚’å‰Šé™¤ã™ã‚‹ã“ã¨ã‚’ãŠã™ã™ã‚ã—ã¾ã™ã€‚
:::

## ã¾ã¨ã‚

Terraformã§Amazon Bedrock Knowledge Base + RAGã‚·ã‚¹ãƒ†ãƒ ã‚’æ§‹ç¯‰ã—ã¾ã—ãŸã€‚

- **OpenSearch Serverless**ã‚’ãƒ™ã‚¯ãƒˆãƒ«ã‚¹ãƒˆã‚¢ã¨ã—ã¦ä½¿ç”¨
- **Titan Embeddings**ã§ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’ãƒ™ã‚¯ãƒˆãƒ«åŒ–
- **Claude 3.5 Sonnet**ã§è‡ªç„¶è¨€èªã«ã‚ˆã‚‹å›ç­”ã‚’ç”Ÿæˆ

Infrastructure as Codeã§ç®¡ç†ã™ã‚‹ã“ã¨ã§ã€ç’°å¢ƒã®å†ç¾æ€§ã‚„æ§‹æˆã®å¯è¦–åŒ–ãŒã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ãŸã€‚

## å‚è€ƒ

- [Deploy Amazon Bedrock Knowledge Bases using Terraform - AWS Blog](https://aws.amazon.com/blogs/machine-learning/deploy-amazon-bedrock-knowledge-bases-using-terraform-for-rag-based-generative-ai-applications/)
- [aws_bedrockagent_knowledge_base - Terraform Registry](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/bedrockagent_knowledge_base)
- [aws_opensearchserverless_collection - Terraform Registry](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/opensearchserverless_collection)
